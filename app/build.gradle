plugins {
    id 'java'
}

apply from: 'tasks.gradle'

repositories {
    mavenCentral()
}

sourceSets {
    e2eTest {
        compileClasspath += sourceSets.main.compileClasspath
        runtimeClasspath += sourceSets.main.runtimeClasspath
        java {
            srcDirs 'src/e2eTest'
        }
    }
}

configurations {
    e2eTestImplementation.extendsFrom(testImplementation)
    e2eTestRuntimeOnly.extendsFrom(testRuntimeOnly)
}

dependencies {
    implementation 'com.amazonaws:aws-java-sdk-dynamodb:1.12.92'
    implementation 'com.amazonaws:aws-java-sdk-s3:1.12.92'
    implementation 'com.amazonaws:aws-lambda-java-core:1.2.1'
    implementation 'com.amazonaws:aws-lambda-java-events:3.9.0'

    implementation 'ca.uhn.hapi.fhir:org.hl7.fhir.r4:5.5.4'
    implementation 'ca.uhn.hapi.fhir:hapi-fhir-structures-r4:5.5.1'

    implementation 'ch.qos.logback:logback-classic:1.2.6'
    implementation 'ch.qos.logback.contrib:logback-jackson:0.1.5'
    implementation 'ch.qos.logback.contrib:logback-json-classic:0.1.5'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.7.2'
    testImplementation 'org.mockito:mockito-junit-jupiter:4.0.0'
    testImplementation 'org.assertj:assertj-core:3.21.0'
    testImplementation 'net.javacrumbs.json-unit:json-unit-assertj:2.28.0'
    testImplementation 'org.apache.commons:commons-lang3:3.12.0'

    e2eTestImplementation 'com.amazonaws:aws-java-sdk-api-gateway:1.12.92'
    e2eTestImplementation 'com.jayway.jsonpath:json-path:2.6.0'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
}

jar {
    from compileJava
    from processResources
    into('lib') {
        from configurations.runtimeClasspath
    }
}

tasks.named('test') {
    useJUnitPlatform()
}

tasks.named('e2eTestClasses') {
    mustRunAfter storeTerraformOutput
}

plugins {
	id 'java'
	id 'java-test-fixtures'
	id "com.diffplug.spotless" version "6.14.0"
	id "org.owasp.dependencycheck" version "8.1.0"
}

repositories {
	mavenCentral()
}

sourceSets {
	integrationTest {
		compileClasspath += sourceSets.main.output
		compileClasspath += sourceSets.test.output
		runtimeClasspath += sourceSets.main.output
		runtimeClasspath += sourceSets.test.output
		java {
			srcDirs 'src/e2eTest'
		}
	}
}

configurations {
	compileClasspath.exclude group: 'commons-logging', module: 'commons-logging'
	runtimeClasspath.exclude group: 'commons-logging', module: 'commons-logging'

	integrationTestImplementation.extendsFrom testImplementation
	integrationTestRuntime.extendsFrom testRuntime
	testFixturesImplementation.extendsFrom implementation
	testFixturesRuntime.extendsFrom runtimeOnly
}

tasks.withType(Test) {
	useJUnitPlatform()
}

dependencies {
	implementation 'com.amazonaws:aws-java-sdk-core:1.12.400'
	implementation 'com.amazonaws:aws-java-sdk-dynamodb:1.12.400'
	implementation 'com.amazonaws:aws-lambda-java-core:1.2.1'
	implementation 'com.amazonaws:aws-lambda-java-events:3.11.0'
	implementation 'com.amazonaws:aws-java-sdk-sqs:1.12.363'
	implementation 'com.amazonaws:aws-java-sdk-ssm:1.12.381'

	implementation 'ca.uhn.hapi.fhir:hapi-fhir-structures-r4:6.2.5'
	implementation 'ca.uhn.hapi.fhir:org.hl7.fhir.r4:6.0.15'
	implementation 'org.apache.logging.log4j:log4j-api:2.20.0'
	implementation 'org.apache.logging.log4j:log4j-core:2.20.0'
	implementation 'com.auth0:java-jwt:4.2.1'

	implementation 'javax.xml.bind:jaxb-api:2.4.0-b180830.0359'

	implementation 'com.fasterxml.jackson.core:jackson-databind:2.14.2'
	implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.13'

	testImplementation 'org.json:json:20220924'
	testImplementation 'org.junit.jupiter:junit-jupiter:5.9.1'
	testImplementation 'org.mockito:mockito-junit-jupiter:4.0.0'
	testImplementation 'uk.org.webcompere:system-stubs-jupiter:2.0.1'
	testImplementation 'org.assertj:assertj-core:3.21.0'
	testImplementation 'net.javacrumbs.json-unit:json-unit-assertj:2.28.0'
	testImplementation 'ch.qos.logback:logback-classic:1.4.5'
	testImplementation 'ch.qos.logback.contrib:logback-json-classic:0.1.5'
	testImplementation 'net.logstash.logback:logstash-logback-encoder:7.0'

	testFixturesImplementation 'com.amazonaws:aws-java-sdk-api-gateway:1.12.92'
	testFixturesImplementation 'com.amazonaws:aws-java-sdk-dynamodb:1.12.400'
	testFixturesImplementation 'org.junit.jupiter:junit-jupiter:5.9.1'

	integrationTestImplementation 'com.amazonaws:aws-java-sdk-api-gateway:1.12.92'
	integrationTestImplementation 'com.jayway.jsonpath:json-path:2.6.0'
	integrationTestImplementation 'org.awaitility:awaitility:4.1.1'
}

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(11)
	}
}

jar {
	from compileJava
	from processResources
}

task buildZip(type: Zip) {
	dependsOn jar
	into('java/lib') {
		from(jar)
		from(configurations.runtimeClasspath)
	}
}

dependencyCheck {
	failBuildOnCVSS = 7
	suppressionFile = './dependency-checks-suppression.xml'
	analyzers {
		assemblyEnabled = false
	}
}

tasks.named('test') {
	useJUnitPlatform()
	environment "AMPLIFY_BASE_URL", "http://localhost:4566"
}

task integrationTest(type: Test) {
	group 'verification'
	description 'Run all end-to-end tests against LocalStack.'
	testClassesDirs = sourceSets.integrationTest.output.classesDirs
	classpath = sourceSets.integrationTest.runtimeClasspath
	outputs.upToDateWhen { false }
}

spotless {
	format 'misc', {
		target '*.gradle'
		trimTrailingWhitespace()
		indentWithTabs()
		endWithNewline()
	}
	java {
		googleJavaFormat('1.15.0').aosp().reflowLongStrings()
	}
}

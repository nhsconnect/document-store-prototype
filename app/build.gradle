plugins {
    id 'java'
}

repositories {
    mavenCentral()
}

sourceSets {
    e2eTest {
        compileClasspath += sourceSets.main.compileClasspath
        runtimeClasspath += sourceSets.main.runtimeClasspath
        java {
            srcDirs 'src/e2eTest'
        }
    }
}

configurations {
    e2eTestImplementation.extendsFrom(testImplementation)
    e2eTestRuntimeOnly.extendsFrom(testRuntimeOnly)
}

dependencies {
    implementation 'com.amazonaws:aws-lambda-java-core:1.2.1'
    implementation 'com.amazonaws:aws-lambda-java-events:3.9.0'
    implementation 'ca.uhn.hapi.fhir:org.hl7.fhir.r4:5.5.4'
    implementation 'ca.uhn.hapi.fhir:hapi-fhir-structures-r4:5.5.1'

    testImplementation 'net.javacrumbs.json-unit:json-unit-assertj:2.28.0'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.7.2'
    testImplementation 'org.assertj:assertj-core:3.21.0'

    e2eTestImplementation 'com.amazonaws:aws-java-sdk-api-gateway:1.12.84'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
}

jar {
    from compileJava
    from processResources
    into('lib') {
        from configurations.runtimeClasspath
    }
}

tasks.named('test') {
    useJUnitPlatform()
}

task e2eTest(type: Test, group: "verification") {
    useJUnitPlatform()

    testClassesDirs = sourceSets.e2eTest.output.classesDirs
    classpath = sourceSets.e2eTest.runtimeClasspath
}

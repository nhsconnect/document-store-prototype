plugins {
	id 'java'
	id "com.diffplug.spotless" version "6.14.0"
	id "org.owasp.dependencycheck" version "8.1.0"

}

repositories {
	mavenCentral()
}

sourceSets {
	integrationTest {
		compileClasspath += sourceSets.main.output
		runtimeClasspath += sourceSets.main.output
	}
}

configurations {
	integrationTestImplementation.extendsFrom implementation
	integrationTestRuntime.extendsFrom runtimeOnly
}

dependencies {
	implementation project(path: ':app')
	implementation 'com.amazonaws:aws-java-sdk-dynamodb:1.12.400'
	implementation 'com.amazonaws:aws-lambda-java-core:1.2.1'
	implementation 'com.amazonaws:aws-lambda-java-events:3.11.0'

	implementation 'ch.qos.logback:logback-classic:1.4.5'
	implementation 'ch.qos.logback.contrib:logback-json-classic:0.1.5'
	implementation 'net.logstash.logback:logstash-logback-encoder:7.0'

	integrationTestImplementation(testFixtures(project(":app")))
	integrationTestImplementation 'org.json:json:20220924'
	integrationTestImplementation 'org.junit.jupiter:junit-jupiter:5.9.1'
	integrationTestImplementation 'org.mockito:mockito-junit-jupiter:4.0.0'
	integrationTestImplementation 'org.mockito:mockito-core:4.0.0'
	integrationTestImplementation 'org.assertj:assertj-core:3.21.0'
	integrationTestImplementation 'com.jayway.jsonpath:json-path:2.6.0'
	integrationTestImplementation 'net.javacrumbs.json-unit:json-unit-assertj:2.28.0'
}

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(11)
	}
}

tasks.withType(Test) {
	useJUnitPlatform()
}

task integrationTest(type: Test) {
	group 'verification'
	description 'Run all end-to-end tests against LocalStack.'
	testClassesDirs = sourceSets.integrationTest.output.classesDirs
	classpath = sourceSets.integrationTest.runtimeClasspath
	outputs.upToDateWhen { false }
}

spotless {
	format 'misc', {
		target '*.gradle'
		trimTrailingWhitespace()
		indentWithTabs()
		endWithNewline()
	}
	java {
		googleJavaFormat('1.15.0').aosp().reflowLongStrings()
	}
}

dependencyCheck {
	failBuildOnCVSS = 7
	suppressionFile = './dependency-checks-suppression.xml'
	analyzers {
		assemblyEnabled = false
	}
}
